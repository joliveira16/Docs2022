
        {
            "cells": [
          {
           "cell_type": "code",
           "metadata": {},
            "execution_count": 1, "outputs": [],
           "source": ["// Last updated 2021-04-29 09:00\n",
"module Portfolio\n",
"\n",
"#r \"nuget: NodaTime\"\n",
"\n",
"open System\n",
"open NodaTime\n",
"\n",
"/// Misc\n",
"/// \n",
"\n",
"// Type extension until NodaTime puts YearMonth.PlusMonths() in the nuget version.\n",
"// taken from here in the NodaTime repo until it is accessible\n",
"// https://github.com/carlosschults/nodatime/blob/43e9f24c2ba5a7ed0fd145c082d9e63cd50b1149/src/NodaTime/YearMonth.cs#L156\n",
"// See https://docs.microsoft.com/en-us/dotnet/fsharp/language-reference/type-extensions\n",
"// to understand type extensions.\n",
"type YearMonth with\n",
"    member this.PlusMonths(months:int) =\n",
"                this.OnDayOfMonth(1).PlusMonths(months).ToYearMonth()\n",
"///\n",
"\n",
"type SecurityId =\n",
"    | Ticker of string\n",
"    | Cusip of string\n",
"    | Bloomberg of string\n",
"    | Permno of int\n",
"    | Other of string\n",
"\n",
"type InvestmentUniverse = \n",
"    { FormationMonth : YearMonth \n",
"      Securities : SecurityId list }\n",
"\n",
"type SecuritySignal = \n",
"    { SecurityId : SecurityId\n",
"      Signal : float }\n",
"\n",
"type SecuritiesWithSignals =\n",
"    { FormationMonth : YearMonth \n",
"      Signals : SecuritySignal list }\n",
"\n",
"type PortfolioId = \n",
"    | Named of string\n",
"    | Indexed of name:string * index:int\n",
"\n",
"type AssignedPortfolio =\n",
"    { PortfolioId : PortfolioId\n",
"      FormationMonth : YearMonth \n",
"      Signals : SecuritySignal list }\n",
"\n",
"\n",
"let assignSignalSort name n (xs: SecuritiesWithSignals) =\n",
"    xs.Signals\n",
"    |\u003e List.sortBy(fun x -\u003e x.Signal)\n",
"    |\u003e List.splitInto n\n",
"    |\u003e List.mapi(fun i ys -\u003e \n",
"        // because lists are 0-indexed and I want the minimum\n",
"        // portfolio index to be 1, I\u0027m doing index = i+1.\n",
"        { PortfolioId = Indexed(name=name,index=i+1)\n",
"          FormationMonth = xs.FormationMonth\n",
"          Signals = ys })\n",
"\n",
"type Position =\n",
"    { SecurityId : SecurityId \n",
"      Weight : float }\n",
"\n",
"type Portfolio = \n",
"    { PortfolioId: PortfolioId\n",
"      FormationMonth : YearMonth\n",
"      Positions : Position list }\n",
"\n",
"type PortfolioReturn =\n",
"    { PortfolioId: PortfolioId\n",
"      YearMonth : YearMonth\n",
"      Return : float }\n",
"\n",
"// This type alias defines the type of a function that has\n",
"// a tuple input of SecurityId * YearMonth and outputs\n",
"// a (SecurityId * float) Option. Think of it like\n",
"// using types to write documentation of the functions.\n",
"type GetsMarketCaps = SecurityId * YearMonth -\u003e (SecurityId * float) Option\n",
"\n",
"// Defining this type alias makes it easier to read the type of the function that I want for\n",
"// marketCapGetter in the function that I have below. Otherwise it might look something like\n",
"// let giveValueWeights (marketCapGetter: (SecurityId * YearMonth -\u003e (SecurityId * float) Option) ...\n",
"// which is the same thing but not as clear what we\u0027re trying to do.\n",
"let giveValueWeights (marketCapGetter: GetsMarketCaps) (x: AssignedPortfolio) =\n",
"    let mktCaps =\n",
"        x.Signals\n",
"        // List.choose throws away None results, so this means \n",
"        // that if the market cap getter returns None\n",
"        // then we will not have that security in our portfolio.\n",
"        |\u003e List.choose(fun signal -\u003e marketCapGetter (signal.SecurityId, x.FormationMonth))\n",
"\n",
"    let totMktCap = mktCaps |\u003e List.sumBy snd\n",
"\n",
"    let pos =\n",
"        mktCaps\n",
"        |\u003e List.map(fun (id, mktCap) -\u003e\n",
"            { SecurityId = id \n",
"              Weight = mktCap / totMktCap })\n",
"    { PortfolioId = x.PortfolioId\n",
"      FormationMonth = x.FormationMonth \n",
"      Positions = pos }\n",
"\n",
"// This type alias defines the type of a function that has\n",
"// a tuple input of SecurityId * YearMonth and outputs\n",
"// a (SecurityId * float). Think of it like\n",
"// using types to write documentation of the functions.\n",
"type GetsReturn = SecurityId * YearMonth -\u003e (SecurityId * float)\n",
"\n",
"// Defining this type alias makes it easier to read the type of the function that I want for\n",
"// marketCapGetter in the function that I have below. Otherwise it might look something like\n",
"// let giveValueWeights (marketCapGetter: (SecurityId * YearMonth -\u003e (SecurityId * float) Option) ...\n",
"// which is the same thing but not as clear what we\u0027re trying to do.\n",
"let getPortfolioReturn (returnGetter: GetsReturn) (x: Portfolio) =\n",
"    let returnMonth = x.FormationMonth.PlusMonths(1)\n",
"    let portRet =\n",
"        x.Positions\n",
"        |\u003e List.sumBy(fun pos -\u003e \n",
"            let (_id, ret) = returnGetter (pos.SecurityId, returnMonth)\n",
"            pos.Weight * ret)\n",
"    { PortfolioId = x.PortfolioId\n",
"      YearMonth = returnMonth\n",
"      Return = portRet }\n",
"\n",
"\n",
"/// This function takes a sample start and sample end\n",
"/// and returns a list with all months from start to end.\n",
"/// Don\u0027t worry about understanding what this function does.\n",
"/// The details are beyond the scope of the class, but if you\u0027re\n",
"/// curious it\u0027s a recursive function:\n",
"/// https://docs.microsoft.com/en-us/dotnet/fsharp/language-reference/functions/recursive-functions-the-rec-keyword\n",
"let getSampleMonths (sampleStart:YearMonth, sampleEnd:YearMonth) =\n",
"    if sampleEnd \u003c= sampleStart then failwith \"sampleEnd should be after sampleStart\"\n",
"    let rec loop (sampleEnd:YearMonth) window = \n",
"        match window with\n",
"        | [] -\u003e failwith \"Need a starting point\"\n",
"        | lastMonth::_monthsBeforeThat -\u003e\n",
"            if lastMonth \u003c sampleEnd then \n",
"                loop sampleEnd (lastMonth.PlusMonths(1)::window)\n",
"            else window\n",
"    loop sampleEnd [sampleStart]\n",
"    |\u003e List.rev\n"]
          }],
            "metadata": {
            "kernelspec": {"display_name": ".NET (F#)", "language": "F#", "name": ".net-fsharp"},
            "langauge_info": {
        "file_extension": ".fs",
        "mimetype": "text/x-fsharp",
        "name": "C#",
        "pygments_lexer": "fsharp",
        "version": "4.5"
        }
        },
            "nbformat": 4,
            "nbformat_minor": 1
        }
        

